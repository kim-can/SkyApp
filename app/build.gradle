apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "sky.skyapp"
        minSdkVersion 14
        //noinspection OldTargetApi
        targetSdkVersion 22
        // 版本
        versionCode 1
        if (project.hasProperty("SKY_VERSION")) {
            if (!project.property("SKY_VERSION").equals("0")) {
                versionCode Integer.valueOf(project.property("SKY_VERSION"))
            }
        }

        // 版本名称
        versionName "1.0"
        if (project.hasProperty("SKY_VERSION_NAME")) {
            if (!project.property("SKY_VERSION_NAME").equals("0")) {
                versionName project.property("SKY_VERSION_NAME")
            }
        }

        //gradle 多渠道打包 默认渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "sky"]
    }
    signingConfigs {
        release {
            storeFile file("../skyappkey.jks")
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug {
            minifyEnabled false

            buildConfigField("int", "SKY", "0")

        }
        release {
            signingConfig signingConfigs.release

            buildConfigField("int", "SKY", "1")

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }

    //gradle 多渠道打包
    productFlavors {

//        jc {}
//        j2w {}

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    android.applicationVariants.all { variant ->
        //删除unaligned包
        variant.assemble.doLast {
            variant.outputs.each { output ->
                println "aligned " + output.outputFile
                println "unaligned " + output.packageApplication.outputFile

                File unaligned = output.packageApplication.outputFile;
                File aligned = output.outputFile
                if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                    println "deleting " + unaligned.getName()
                    unaligned.delete()
                }
            }
        }
        //输出格式
        variant.outputs.each {
            def file = it.outputFile
            it.outputFile = new File(file.parent, file.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.github.skyJinc:sky:2.0'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'

    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
}
